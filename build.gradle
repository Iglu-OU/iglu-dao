buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'io.spring.dependency-management' version '0.6.1.RELEASE'
}

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
}

dependencies {
    compileOnly(
            'org.projectlombok:lombok',
    )

    compile(
            'org.springframework:spring-jdbc',
            'org.slf4j:slf4j-api',
            'com.google.guava:guava:19.0',
    )

    testCompileOnly(
            'org.projectlombok:lombok',
    )

    testCompile(
            'org.springframework.boot:spring-boot-starter-jdbc',
            'org.springframework.boot:spring-boot-starter-test',
    )

    testRuntime(
            'org.hsqldb:hsqldb',
            'org.postgresql:postgresql:9.4.1211',
    )
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

// from http://jdpgrailsdev.github.io/blog/2014/10/28/gradle_resolve_all_dependencies.html
task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                configuration.resolve()
            }
            subProject.configurations.each { configuration ->
                configuration.resolve()
            }
        }
    }
}
